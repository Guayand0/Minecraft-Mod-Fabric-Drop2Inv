plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id "com.gradleup.shadow" version "9.2.2"
}

base {
	archivesName = project.archives_base_name
}

version = project.mod_version
var semver_version = version + "+mc" + project.minecraft_version
group = project.maven_group

sourceSets.main.java.srcDirs += 'java'
sourceSets.main.resources.srcDirs += 'resources'

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/" }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	//mappings loom.officialMojangMappings()
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// ModMenu
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	// Cloth Config
	modCompileOnlyApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	implementation (shadow "blue.endless:jankson:1.2.3")
}

processResources {
	inputs.property "version", version

	filesMatching("fabric.mod.json") {
		expand "version": semver_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

shadowJar {
	relocate "blue.endless.jankson", "${group}.shadowed.blue.endless.jankson"
	configurations = [project.configurations.shadow]
	archiveClassifier = "shadow"

	destinationDirectory = layout.buildDirectory.dir('shadowlibs')
}

remapJar {
	input.set(shadowJar.archiveFile)
	dependsOn(shadowJar)

	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

artifacts {
	archives sourcesJar
}